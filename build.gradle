import java.util.concurrent.TimeUnit;

/*
 * Copyright (c) 2014 fStream. All Rights Reserved.
 *
 * Project and contact information: https://bitbucket.org/fstream/fstream
 *
 * Unauthorized copying of this file, via any medium is strictly prohibited.
 * Proprietary and confidential.
 */
 
defaultTasks 'build'
 
buildscript {
	
   //
   // Versions
   //
   ext.springVersion     = '4.0.5.RELEASE'
   ext.springBootVersion = "1.1.0.RC1"
   ext.quickfixjVersion  = '1.5.3'
   ext.camelVersion      = '2.13.1'
   ext.kafkaVersion      = '0.8.1'
   ext.stormVersion      = '0.9.2-incubating-SNAPSHOT'
   ext.hbaseVersion      = '0.98.2-hadoop1'
   ext.hadoopVersion     = '1.2.1'
   ext.esperVersion      = '4.11.0'
   ext.curatorVersion    = '2.4.2'
   ext.guavaVersion      = '17.0'
   ext.jacksonVersion    = '2.3.3'
   ext.lombokVersion     = '1.12.6'
   
   //
   // Libraries
   //
   
   ext.libraries = [
	   guava:              "com.google.guava:guava:$guavaVersion",
	   springBoot:         "org.springframework.boot:spring-boot:$springBootVersion",
	   springBootActuator: "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion",
	   springBootWeb:      "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
	   jacksonDatabind:    "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
	   jacksonJoda:        "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion",
	   kafka:              "org.apache.kafka:kafka_2.10:$kafkaVersion",
	   storm:              "org.apache.storm:storm-core:${stormVersion}",
	   stormKafka:         "net.wurstmeister.storm:storm-kafka-0.8-plus:0.4.0",
	   esper:              "com.espertech:esper:${esperVersion}",
       hadoop:             "org.apache.hadoop:hadoop-core:$hadoopVersion",
	   hadoopTest:         "org.apache.hadoop:hadoop-test:$hadoopVersion",
	   hbaseClient:        "org.apache.hbase:hbase-client:$hbaseVersion",
	   curator:            "org.apache.curator:curator-recipes:$curatorVersion",
	   curatorTest:        "org.apache.curator:curator-test:$curatorVersion"
   ]
   
   repositories {
	   mavenLocal()
       maven { url "http://repo.spring.io/libs-milestone" }
       maven { url "http://repo.spring.io/libs-snapshot" }
   }
   
   dependencies {
	   classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	   classpath 'org.ajoberstar:gradle-git:0.8.0'
   }
}

subprojects {
	version = '0.0.1-SNAPSHOT'
	group = 'io.fstream'
	
	apply plugin: 'java'
	
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_7
	
	repositories { 
		mavenCentral() 
		maven { url "https://seqwaremaven.oicr.on.ca/artifactory/dcc-dependencies" }
		maven { url "http://repo.spring.io/libs-milestone" }
		maven { url "http://repo.spring.io/libs-snapshot" }
	    maven { url 'http://repo.marketcetera.org/maven' }
	    maven { url 'http://clojars.org/repo' }
	    maven { url 'http://conjars.org/repo' }
	    maven { url 'http://twitter4j.org/maven2' }
	}
	
	configurations {
		all*.exclude group: 'org.slf4j', module: 'slf4j-simple'
		all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		all*.exclude group: 'log4j', module: 'log4j'
		all*.exclude group: 'javax.jms', module: 'jms'
		all*.exclude group: 'com.sun.jdmk', module: 'jmxtools'
		all*.exclude group: 'com.sun.jmx', module: 'jmxri'
	}
	
	dependencies {
		// Compile-time transformations
		compile "org.projectlombok:lombok:$lombokVersion"
		
		// Utilities
		compile 'joda-time:joda-time:2.3'
		
		// Logging
		compile 'ch.qos.logback:logback-classic:1.1.2'
		compile 'org.slf4j:jcl-over-slf4j:1.7.6'
		compile 'org.slf4j:log4j-over-slf4j:1.7.6'
		
		// Test
		testCompile 'junit:junit:4.11'
		testCompile 'org.assertj:assertj-core:1.6.0'
		testCompile 'org.mockito:mockito-all:1.9.5'
	}
	
	jar {
		manifest {
			attributes 'Provider': 'gradle', 'Implementation-Title': project.name, 'Implementation-Version': project.version
		}
	}
}


/**
 * Run fStream in "vagrant up / fig up" style.
 */
task up << {
	addShutdownHook {
		println "**** Shutting down..."
		println "Waiting ${seconds}s ..."
		TimeUnit.SECONDS.sleep(seconds)
	}
	
	def tasks = project.getAllTasks(true).values().flatten().findAll { it.name == "bootRun" }.collectEntries{[it.project.name, it]}
	def seconds = 10
	
	['fstream-test', 'fstream-rates', 'fstream-compute', 'fstream-web'].each { projectName ->
		Thread.start {
			println "Running $projectName..."
			tasks[projectName].execute()
		}

		println "Waiting ${seconds}s ..."
		TimeUnit.SECONDS.sleep(seconds)
	}

	TimeUnit.DAYS.sleep(1)
}
